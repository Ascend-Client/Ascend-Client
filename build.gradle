import org.gradle.api.services.BuildService

import java.nio.file.Files
import java.nio.file.StandardCopyOption
import java.time.Duration
import java.util.jar.JarFile
import java.util.jar.JarOutputStream
import java.util.zip.ZipEntry

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("io.github.goooler.shadow:shadow-gradle-plugin:8.1.8")
    }
}

plugins {
    id 'java'
    id("com.gorylenko.gradle-git-properties") version "2.4.1"
}

group 'io.github.betterclient'
version '1.0'

repositories {
    mavenCentral()

    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
}

File downloadQuixotic() {
    File f = file("quixotic.jar")
    if(f.exists()) return f
    println("Downloading quixotic")

    f.createNewFile()
    def is = new URI("https://github.com/betterclient/Quixotic-Loader/releases/download/ithinkiforgoralreadyexists/Quixotic-1.0.jar").toURL().openStream()
    Files.copy(
            is,
            f.toPath(),
            StandardCopyOption.REPLACE_EXISTING)

    return f
}

def asmVersion = "9.7"

dependencies {
    implementation files(downloadQuixotic())

    implementation "org.json:json:20231013"
    implementation "net.fabricmc:sponge-mixin:0.15.0+mixin.0.8.7"
    implementation "io.github.llamalad7:mixinextras-fabric:0.4.0"
    implementation "net.fabricmc:tiny-remapper:0.10.1"

    implementation "org.ow2.asm:asm:$asmVersion"
    implementation "org.ow2.asm:asm-analysis:$asmVersion"
    implementation "org.ow2.asm:asm-commons:$asmVersion"
    implementation "org.ow2.asm:asm-tree:$asmVersion"
    implementation "org.ow2.asm:asm-util:$asmVersion"

    annotationProcessor "org.ow2.asm:asm:$asmVersion"
    annotationProcessor "org.ow2.asm:asm-analysis:$asmVersion"
    annotationProcessor "org.ow2.asm:asm-commons:$asmVersion"
    annotationProcessor "org.ow2.asm:asm-tree:$asmVersion"
    annotationProcessor "org.ow2.asm:asm-util:$asmVersion"
}

tasks.processResources {
    dependsOn(generateGitProperties)
}

gitProperties {
    gitPropertiesName = "github.txt"
    gitPropertiesResourceDir = file("src/main/resources/ballsack/github")
    keys = ["git.branch", "git.commit.id.abbrev"]
}

allprojects {
    targetCompatibility = sourceCompatibility = 21
}

subprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: "io.github.goooler.shadow"

    configurations {
        shade
        implementation.extendsFrom(shade)
    }

    dependencies {
        if(project.name == "Version-Auto-Updater") return

        shade parent
        shade files(downloadQuixotic())

        shade "org.json:json:20231013"
        shade "net.fabricmc:sponge-mixin:0.15.0+mixin.0.8.7"
        shade "io.github.llamalad7:mixinextras-fabric:0.4.0"
        shade "net.fabricmc:tiny-remapper:0.10.1"

        shade "org.ow2.asm:asm:$asmVersion"
        shade "org.ow2.asm:asm-analysis:$asmVersion"
        shade "org.ow2.asm:asm-commons:$asmVersion"
        shade "org.ow2.asm:asm-tree:$asmVersion"
        shade "org.ow2.asm:asm-util:$asmVersion"

        annotationProcessor "org.ow2.asm:asm:$asmVersion"
        annotationProcessor "org.ow2.asm:asm-analysis:$asmVersion"
        annotationProcessor "org.ow2.asm:asm-commons:$asmVersion"
        annotationProcessor "org.ow2.asm:asm-tree:$asmVersion"
        annotationProcessor "org.ow2.asm:asm-util:$asmVersion"
    }

    shadowJar {
        dependencies {}
        configurations = [project.configurations.shade]
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        archiveClassifier = project.objects.property(String.class).convention("all")
    }
}

abstract class MergeJarsTask extends DefaultTask {
    @TaskAction
    //@NotStatic
    void taskAction() {
        println("Merging jars!")
        var versions = new String[] {"1.16-combat-6", "1.19.4", "1.20.6"}

        for (final def ver in versions) {
            println("Merge: ${ver}.jar with ${ver}-deps.jar")
            var mapFiles = new HashMap<String, byte[]>()

            readFile(project.file("${ver}-deps.jar") as File, mapFiles)
            //read normal after deps so it overrides the deps
            readFile(project.file("${ver}.jar") as File, mapFiles)

            var finalOut = project.file("${ver}.jar") as File
            var fos = new JarOutputStream(new FileOutputStream(finalOut))
            for (final def k in mapFiles.keySet()) {
                fos.putNextEntry(new ZipEntry(k))
                fos.write(mapFiles[k])
                fos.closeEntry()
            }
            fos.close()
        }

        println("Done!")
    }

    static void readFile(File f, Map<String, byte[]> mapFiles) {
        var jf = new JarFile(f)
        var jes = jf.entries()

        while (jes.hasMoreElements()) {
            var je = jes.nextElement()

            if(!je.directory && je.name != "fabric.mod.json") {
                var str = jf.getInputStream(je)
                mapFiles[je.name] = str.readAllBytes()
                str.close()
            }
        }

        jf.close()
    }
}

tasks.create("mergeJars", MergeJarsTask.class)