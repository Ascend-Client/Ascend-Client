import java.nio.file.Files
import java.nio.file.StandardCopyOption

plugins {
    id 'java'
    id "fabric-loom" version "1.1-SNAPSHOT"
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id("com.gorylenko.gradle-git-properties") version "2.4.1"
}

group 'io.github.betterclient'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()

    maven {
        url "https://repo.spongepowered.org/repository/maven-public/"
    }

    maven {
        name "CombatReforged"
        url "https://maven.rizecookey.net"
    }
}


def mcVer = "1.16_combat-6"

configurations {
    shade
    implementation.extendsFrom(shade)
}

loom {
    setCustomMinecraftManifest("https://gist.githubusercontent.com/rizecookey/4c6142baaccc3875f9b227fe22f2ace5/raw/c8ed74b19f7a5315813c9d4b199798b692a8f359/1.16_combat-6.json")
    setIntermediaryUrl("https://maven.rizecookey.net/net/fabricmc/intermediary/${mcVer}/intermediary-${mcVer}-v2.jar")

    runConfigs.configureEach {
        client()
        setDefaultMainClass("io.github.betterclient.client.launch.IDEMain")
    }
}

File downloadQuixotic() {
    File f = file("quixotic.jar") as File
    if(f.exists()) return f

    f.createNewFile()
    def is = new URL("https://github.com/betterclient/Quixotic-Loader/releases/download/valedier/Quixotic-1.0.jar").openStream()
    Files.copy(
            is,
            f.toPath(),
            StandardCopyOption.REPLACE_EXISTING)

    return f
}

def asmVersion = "9.5"

dependencies {
    minecraft "com.mojang:minecraft:$mcVer"
    mappings "net.fabricmc:yarn:1.16_combat-6+build.2:v2"

    shade files(downloadQuixotic())

    shade 'org.json:json:20230227'
    shade "org.spongepowered:mixin:0.8.5"

    shade "org.ow2.asm:asm:$asmVersion"
    shade "org.ow2.asm:asm-analysis:$asmVersion"
    shade "org.ow2.asm:asm-commons:$asmVersion"
    shade "org.ow2.asm:asm-tree:$asmVersion"
    shade "org.ow2.asm:asm-util:$asmVersion"
}

tasks.dependencies {
    dependsOn(generateGitProperties)
}

shadowJar {
    dependencies {

    }
    configurations = [project.configurations.shade]

    duplicatesStrategy DuplicatesStrategy.EXCLUDE //prevent duplicates
    classifier "" //prevent creation of unshadowed jar
}

gitProperties {
    gitPropertiesName = "github.txt"
    gitPropertiesResourceDir = file("src/main/resources/ballsack/github")
    keys = ["git.branch", "git.commit.id.abbrev"]
}